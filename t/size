#!/usr/bin/perl
# This may appear arbitrary, but DO NOT CHANGE IT.
# Debhelper is supposed to consist of small, simple, easy to understand
# programs. Programs growing in size and complexity without bounds is a
# bug.
use Test::More;

# exclude dh_python since it's deprecated
my @progs=grep { -x $_ && !/dh_python/ } glob("dh_*");

plan(tests => (@progs + @progs));

foreach my $file (@progs) {

	my $lines=0;
	my $maxlength=0;
	open(IN, $file) || die "open: $!";
	my $cutting=0;
	while (<IN>) {
		$cutting=1 if /^=/;
		$cutting=0 if /^=cut/;
		next if $cutting || /^(=|\s*\#)/;
		$lines++;
		$maxlength=length($_) if length($_) > $maxlength;
	}
	close IN;
	print "# $file has $lines lines, max length is $maxlength\n";
	ok($lines < 200, $file);
	ok($maxlength < 160, $file);
}
