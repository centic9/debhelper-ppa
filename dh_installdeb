#!/usr/bin/perl -w

=head1 NAME

dh_installdeb - install files into the DEBIAN directory

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_installdeb> [S<I<debhelper options>>]

=head1 DESCRIPTION

dh_installdeb is a debhelper program that is responsible for installing
files into the DEBIAN directories in package build directories with the
correct permissions.

=head1 FILES

=over 4

=item I<package>.postinst

=item I<package>.preinst

=item I<package>.postrm

=item I<package>.prerm

These maintainer scripts are installed into the DEBIAN directory.

Inside the scripts, the token B<#DEBHELPER#> is replaced with
shell script snippets generated by other debhelper commands.

=item I<package>.triggers

=item I<package>.shlibs

These control files are installed into the DEBIAN directory.

=item I<package>.conffiles

This control file will be installed into the DEBIAN directory.

In V3 compatibility mode and higher, all files in the etc/ directory in a
package will automatically be flagged as conffiles by this program, so
there is no need to list them manually here.

=back

=cut

init();

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);

	if (! -d "$tmp/DEBIAN") {
		doit("install","-o",0,"-g",0,"-d","$tmp/DEBIAN");
	}

	if (is_udeb($package)) {
		# For udebs, only do the postinst, and no #DEBHELPER#.
		# Udebs also support menutest and isinstallable scripts.
		foreach my $script (qw{postinst menutest isinstallable}) {
			my $f=pkgfile($package,$script);
			if ($f) {
				doit("install", "-o", 0, "-g", 0, "-m", 755, 
				     $f, "$tmp/DEBIAN/$script");
			}
		}
		next;		
	}
	
	# Install debian scripts.
	foreach my $script (qw{postinst preinst prerm postrm}) {
		debhelper_script_subst($package, $script);
	}

	if (! is_udeb($package)) {
		# Install non-executable files
		foreach my $file (qw{shlibs conffiles triggers}) {
			my $f=pkgfile($package,$file);
			if ($f) {
				doit("install","-o",0,"-g",0,"-m",644,"-p",$f,"$tmp/DEBIAN/$file");
			}
		}
	}

	# Automatic conffiles registration: If it is in /etc, it is a
	# conffile.
	if (! compat(2) && -d "$tmp/etc" && ! is_udeb($package)) {
		complex_doit("find $tmp/etc -type f -printf '/etc/%P\n' >> $tmp/DEBIAN/conffiles");
		# Anything found?
		if (-z "$tmp/DEBIAN/conffiles") {
			doit("rm", "-f", "$tmp/DEBIAN/conffiles");
		}
		else {
			doit("chmod", 644, "$tmp/DEBIAN/conffiles");
		}
	}
}

=head1 SEE ALSO

L<debhelper(7)>

This program is a part of debhelper.

=head1 AUTHOR

Joey Hess <joeyh@debian.org>

=cut
